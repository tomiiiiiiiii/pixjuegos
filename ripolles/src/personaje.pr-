Process personaje(jugador,tipo);
Private
	pulsando_salto;
	pulsando_ataque1;
	hacia_que_lado;
	inercia_max_x;
	inercia_max_y;
	inercia_max_x_actual;
	inercia_max_y_actual;
	fuerza_ataque;
	invencibilidad;
	pulsando_abajo;
	pulsando_arriba;
	no_pulsando_lado;
	
	sabe_defenderse=0;
	sabe_evadir=0;
	sabe_saltar=0;
	sabe_uppercut=0;
	sabe_ataque_especial=0;
	sabe_ataca_aire=0;
	sabe_ataque_fuerte=0;
	sabe_correr=0;
	sabe_combo=0;
	sabe_moverse=1; //solo el jefe 4 no se mueve
	sabe_herida_grave=1;
	
	retraso_movimiento_defensa;
	si_ataca;
	id_enemigo_cogido;
	rotura_cogido;
	tres_ataques;
	ultimo_ataque=10;
	ataca_fuerte_sin_retraso=0;	
	tipo_original;
Begin
	resolution=global_resolution;
	ctype=coordenadas;
	p[jugador].juega=1;
	p[jugador].identificador=id;

	rango=20;
	
	tipo_original=tipo;
	if(tipo==0)
		sabe_correr=1;
		sabe_defenderse=1;
		sabe_saltar=1;
		sabe_combo=1;
		sabe_ataque_especial=1;
		sabe_uppercut=1;
		sabe_ataca_aire=1;
		sabe_ataque_fuerte=1;
		sabe_evadir=1;
		switch(modo_juego)
			case modo_historia:
				x=50+50*jugador;
			end
			case modo_supervivencia:
				x=ancho_pantalla/2;
			end
			case modo_battleroyale:
				if(jugador<3)
					x=50;
				else
					x=ancho_pantalla-50;
				end
			end
		end
		if(modo_juego==modo_historia)
			y_base=130+40*jugador;
		else
			if(jugador==1 or jugador==3)
				y_base=135;
			else
				y_base=360;
			end
		end
		controlador(jugador);
		switch(jugador)
			case 1: 
				if(ops.truco_pato==1)
					file=fpg_pato;
				else
					if(en_moto)
						file=fpg_ripolles1bici;
					else
						file=fpg_ripolles1;
					end
				end
			end
			case 2: 
				if(en_moto)
					file=fpg_ripolles2bici;
				else
					file=fpg_ripolles2;
				end
			end
			case 3: 
				if(en_moto)
					file=fpg_ripolles3bici;
				else
					file=fpg_ripolles3;
				end
			end
			case 4: 
				if(en_moto)
					file=fpg_ripolles4bici;
				else
					file=fpg_ripolles4;
				end
			end
		end
		p[jugador].vida=100;
		inercia_max_x=6;
		inercia_max_y=3;
		fuerza_ataque=10;
		marcador(jugador);
		if(ops.truco_pato==1)
			inercia_max_x=9;
			inercia_max_y=6;
			fuerza_ataque=40;
		end
		if(en_moto)
			altura=-100;
			gravedad=-20;
			x_inc=60;
			x=-200;
			angle_inc=-15;
			angle=220000;
		else
			altura=-300;
		end
	else
		enemigos++;
		x=rand(0,640);
		y_base=rand(100,360);
		switch(tipo)
			case 1:
				inercia_max_x=3;
				inercia_max_y=1;
				fuerza_ataque=6;
				file=fpg_enemigo1; 
				p[jugador].vida=50;
			end
			case 2:
				inercia_max_x=4;
				inercia_max_y=2;
				fuerza_ataque=10;
				file=fpg_enemigo2; 
				p[jugador].vida=150;
				sabe_defenderse=1;
			end
			case 3:
				inercia_max_x=2;
				inercia_max_y=2;
				fuerza_ataque=15;
				file=fpg_enemigo3; 
				p[jugador].vida=100;
			end
			case 4:
				inercia_max_x=5;
				inercia_max_y=3;
				fuerza_ataque=20;
				file=fpg_enemigo4; 
				p[jugador].vida=120;
				sabe_saltar=1;
				sabe_ataca_aire=1;
			end
			case 5: //EVIL RIPOLLES
				inercia_max_x=7;
				inercia_max_y=4;
				fuerza_ataque=10;
				file=fpg_enemigo5;
				p[jugador].vida=300;
				sabe_defenderse=1;
				sabe_saltar=1;
				sabe_ataca_aire=1;
			end
			case 6: //bailarina
				inercia_max_x=4;
				inercia_max_y=4;
				fuerza_ataque=10;
				file=fpg_enemigo6;
				p[jugador].vida=50;
				sabe_ataque_especial=1;
			end
			case 7: //pez
				inercia_max_x=4;
				inercia_max_y=4;
				fuerza_ataque=20;
				file=fpg_enemigo7;
				p[jugador].vida=90;
				lleva_objeto=100;
			end
			case 102: //JEFE 2
				inercia_max_x=6;
				inercia_max_y=6;
				fuerza_ataque=20;
				fpg_jefe=load_fpg("fpg/jefe2.fpg");
				file=fpg_jefe;
				p[jugador].vida=300;
				sabe_ataque_especial=1;
				alpha=0;
			end
			case 104: //JEFE 4
				inercia_max_x=20;
				inercia_max_y=20;
				fuerza_ataque=20;
				fpg_jefe=load_fpg("fpg/jefe4.fpg");
				file=fpg_jefe;
				p[jugador].vida=400;
				sabe_ataque_especial=1;
				sabe_saltar=1;
				sabe_moverse=0;
				sabe_herida_grave=0;
				alpha=0;
			end
		end
		if(jugadores>1)
			p[jugador].vida+=p[jugador].vida*(jugadores*0.2);
		end
		if(good_vs_evil)
			fuerza_ataque=fuerza_ataque/3;
			p[jugador].vida=p[jugador].vida*0.7;
		end
	end

	if(en_moto)
		sabe_correr=0;
		if(tipo!=0) sabe_defenderse=0; end
		sabe_evadir=0;
		sabe_ataque_especial=0;
		sabe_combo=0;
		sabe_uppercut=0;
		sabe_ataca_aire=0;
		sabe_ataque_fuerte=0;
	end
	
	if(jugador==100) marcador_jefe(); end
	
	recoloca_centros_personaje(file);
	
	accion=quieto;
	alto=graphic_info(file,1,G_HEIGHT);
	frame;
	
	if(tipo==102) from alpha=0 to 255 step 25; frame; end end
	
	loop
		if(evento_hamburguesa==12) //everyone is super
			inercia_max_x=9;
			inercia_max_y=6;
			fuerza_ataque=40;
		end
		if(tipo!=tipo_original) //efecto hamburguesa!
			tipo_original=tipo;

			//reset de habilidades
			sabe_defenderse=0;
			sabe_evadir=0;
			sabe_saltar=0;
			sabe_uppercut=0;
			sabe_ataque_especial=0;
			sabe_ataca_aire=0;
			sabe_ataque_fuerte=0;
			sabe_correr=0;
			sabe_combo=0;

			switch(tipo)
				case 0:
					sabe_correr=1;
					sabe_defenderse=1;
					sabe_saltar=1;
					sabe_combo=1;
					sabe_ataque_especial=1;
					sabe_uppercut=1;
					sabe_ataca_aire=1;
					sabe_ataque_fuerte=1;
					sabe_evadir=1;
					if(jugador>9)
						switch(rand(1,4))
							case 1: file=fpg_ripolles1;	end
							case 2: file=fpg_ripolles2;	end
							case 3: file=fpg_ripolles3;	end
							case 4: file=fpg_ripolles4;	end
						end
					else
						switch(jugador)
							case 1: 
								if(ops.truco_pato==1)
									file=fpg_pato;
								else
									file=fpg_ripolles1;
								end
							end
							case 2: file=fpg_ripolles2;	end
							case 3: file=fpg_ripolles3;	end
							case 4: file=fpg_ripolles4;	end
						end
					end
					if(ops.truco_pato==1)
						inercia_max_x=9;
						inercia_max_y=6;
						fuerza_ataque=40;
					else
						inercia_max_x=6;
						inercia_max_y=3;
						fuerza_ataque=10;
					end
				end
				case 1:
					inercia_max_x=3;
					inercia_max_y=1;
					fuerza_ataque=6;
					file=fpg_enemigo1; 
				end
				case 2:
					inercia_max_x=4;
					inercia_max_y=2;
					fuerza_ataque=10;
					file=fpg_enemigo2; 
					sabe_defenderse=1;
				end
				case 3:
					inercia_max_x=2;
					inercia_max_y=2;
					fuerza_ataque=15;
					file=fpg_enemigo3; 
				end
				case 4:
					inercia_max_x=5;
					inercia_max_y=3;
					fuerza_ataque=20;
					file=fpg_enemigo4; 
					sabe_saltar=1;
					sabe_ataca_aire=1;
				end
				case 5: //EVIL RIPOLLES
					inercia_max_x=7;
					inercia_max_y=4;
					fuerza_ataque=10;
					file=fpg_enemigo5;
					sabe_defenderse=1;
					sabe_saltar=1;
					sabe_ataca_aire=1;
				end
				case 6: //bailarina
					inercia_max_x=4;
					inercia_max_y=4;
					fuerza_ataque=10;
					file=fpg_enemigo6;
					sabe_ataque_especial=1;
				end
				case 7: //pez
					inercia_max_x=4;
					inercia_max_y=4;
					fuerza_ataque=20;
					file=fpg_enemigo7;
					lleva_objeto=100;
				end
			end
			alto=graphic_info(file,1,G_HEIGHT);
		end
		if(tipo==0)
			//say("ACCION:"+accion+" | ANIMACION:"+animacion+" | GRAPH:"+graph);
			//while(!ready) frame; end
			while(!ready and !ganando) frame; end
		else
			while(!ready and !ganando) frame; end
		end

		if(en_moto and tipo>0 and x<-100)
			break;
		end

		if(en_moto and accion==defiende)
			nube_humo(x+50,y+35,z,-10,0,rand(70,100));
			nube_humo(x-10,y+50,z,-10,0,rand(70,100));
			x_inc-=2;
		end
		
		rotura_cogido=0;
		while(accion==cogido)
			if(rotura_cogido<300) 
				rotura_cogido++; 
			else
				accion=quieto;
				break;
			end
			frame; 
		end
		
		if(sabe_combo)
			if(accion!=ataca_suelo)
				if(ultimo_ataque<10)
					ultimo_ataque++;
				else
					tres_ataques=0;
				end
			end
		else
			tres_ataques=0;
		end
			
		if(p[jugador].vida<1 or (tipo>0 and ganando))
			p[jugador].vida=0;
			if(accion!=muere)
				gravedad=-20;
				if(abs(x_inc)<7)
					if(flags==1) x_inc=7; else x_inc=-7; end
				end
				if(tipo==0) sonido(31,0); end
			end
			accion=muere; herida+=100; 
		end
		
		if(tipo==0 and lleva_objeto!=0 and accion==quieto)
			if(lleva_objeto==obj_rosquilleta or lleva_objeto==obj_rollo or lleva_objeto==obj_naranja or lleva_objeto==obj_pescado) //comida
				if(lleva_objeto==obj_rollo)
					p[jugador].vida=100;
				elseif(lleva_objeto==obj_naranja or lleva_objeto==obj_pescado)
					switch(rand(1,2))
						case 1: 
							mensaje_player(textos[33]);
							inercia_max_x++; inercia_max_y++; 
						end
						case 2: 
							mensaje_player(textos[34]);
							fuerza_ataque+=10; 
						end
					end
				else
					p[jugador].vida+=50;
					if(p[jugador].vida>100) p[jugador].vida=100; end
				end
				lleva_objeto=0;
			end
			if(lleva_objeto==obj_casco) //casco de Jaume I
				invencibilidad=900;
				p[jugador].vida=100;
				lleva_objeto=0;
			end
			if(lleva_objeto==obj_hamburguesa)
				efecto_hamburguesa();
				lleva_objeto=0;
			end
		end

		if(ganando_moto and altura==0 and angle==0)
			while(x<ancho_pantalla+200)
				x_inc++;
				animame();
				x+=x_inc/3;
				frame(100-x_inc);
			end
			return;
		end
		if(ganando and tipo==0 and altura==0)
			graph=161;
			accion=quieto;
			while(ganando==1)
				if(altura==0 and gravedad==0)
					if(rand(0,20)==0) 
						gravedad=-10; 
						frame; 
					end
				elseif(altura>1)
					nube_humo(x,y+50,z,0,0,100);
					altura=0;
					gravedad=0;
					y=y_base;
				else
					altura+=gravedad;
					gravedad++;
				end
				sombra();
				frame; 
			end
		end

		if(flags==0)
			hacia_que_lado=1;
		else
			hacia_que_lado=-1;
		end

		if(invencibilidad>0)
			alpha=128;
			invencibilidad--;
			herida=0;
		else
			if(alpha<255) alpha+=5; end
		end
		
		if(herida==0 and (accion==herido_leve or accion==herido_grave))
			accion=quieto;
		end
		
		if(accion==defiende and !p[jugador].botones[b_3])
			accion=quieto;
		end
		
		//cuando se mueve defendiéndose, da pequeños golpes para mejorar la escapada
		if(accion==defiende and altura>0)
			ataque(x,y,file,graph,1,20,jugador);
		end
		
		if(p[jugador].botones[b_abajo])
			pulsando_abajo++;
		else
			pulsando_abajo=0;
		end
		
		if(p[jugador].botones[b_arriba])
			pulsando_arriba++;
		else
			pulsando_arriba=0;
		end
		
		//para permitir correr
		if(p[jugador].botones[b_izquierda] or p[jugador].botones[b_derecha])
			if(no_pulsando_lado<2) no_pulsando_lado++; end
		else
			if(no_pulsando_lado>0) no_pulsando_lado--; end
		end
		
		if(tipo>0 and tipo<5) //evitamos que los enemigos puedan hacer ataques especiales o correr, salvo evil ripo
			pulsando_abajo=5;
			pulsando_arriba=5;
			no_pulsando_lado=5;
		end
		
		if(accion==ataca_area) herida=0; end
		
		if(retraso_movimiento_defensa>0) retraso_movimiento_defensa--; end
		
		if(herida==0)
			if(altura==0) //EN EL SUELO
				if(accion==defiende and sabe_evadir==1) //ESQUIVAR
					if(retraso_movimiento_defensa==0)
						//movimiento
						if(p[jugador].botones[b_arriba] xor p[jugador].botones[b_abajo])
							if(p[jugador].botones[b_arriba])
								y_inc-=10;
								gravedad=-15;
								altura=1;
								retraso_movimiento_defensa=0;
								nube_humo(x+(hacia_que_lado*30),y+50,z,2*hacia_que_lado,0,100);
							else //abajo
								y_inc=10;
								gravedad=-15;
								altura=1;
								retraso_movimiento_defensa=0;
								nube_humo(x+(hacia_que_lado*30),y+50,z,2*hacia_que_lado,0,100);
							end
						end
						
						if(p[jugador].botones[b_izquierda] xor p[jugador].botones[b_derecha])
							if(p[jugador].botones[b_izquierda])
								x_inc-=10;
								gravedad=-10;
								altura=1;
								retraso_movimiento_defensa=0;
								flags=0;
								nube_humo(x+(hacia_que_lado*30),y+50,z,2*hacia_que_lado,0,100);
							else //derecha
								x_inc+=10;
								gravedad=-10;
								altura=1;
								retraso_movimiento_defensa=0;
								if(!en_moto) flags=1; end
								nube_humo(x+(hacia_que_lado*30),y+50,z,2*hacia_que_lado,0,100);
							end
						end
					end
				end
		
				if((accion==quieto or accion==camina or accion==corre))
					//ATAQUE SUELO O LANZAR OBJETO
					if(p[jugador].botones[b_1] and pulsando_ataque1==0)
						pulsando_ataque1=1;
						if(lleva_objeto==0)
							if(p[jugador].botones[b_3] and sabe_ataque_especial and p[jugador].vida>20)
								accion=ataca_area;
								sonido(3,3);
								if(invencibilidad==0 and tipo==0)
									p[jugador].vida-=10;
								end
							else
								si_ataca=0;
								if(accion==quieto and x_inc==0 and (id_col=collision(type objeto)))
									repeat
										//si el objeto está en el suelo y no somos un enemigo cogiendo vida o powerup...
										if(id_col.y>y+35) 
											lleva_objeto=id_col.graph;
											accion=coge_objeto;
											signal(id_col,s_kill);
										else
											si_ataca=1;
										end
									until(!(id_col=collision(type objeto)) or lleva_objeto!=0)
								elseif(accion==quieto and x_inc==0 and (id_col=collision(type personaje)))
									//si el enemigo está en el suelo herido_grave
									if(en_rango(z,id_col.z,40) and id_col.accion==herido_grave and id_col.graph==151 and id_col.altura==0 and id_col.tipo<100)
										id_col.graph=151;
										lleva_objeto=-1;
										accion=coge_objeto;
										id_col.accion=cogido;
										id_enemigo_cogido=id_col;
									else
										si_ataca=1;
									end
								else
									si_ataca=1;
								end
								if(si_ataca)
									sonido(3,0);
									if(p[jugador].botones[b_abajo] and pulsando_abajo<3 and sabe_ataque_fuerte)
										accion=ataca_fuerte;
									elseif(p[jugador].botones[b_arriba] and pulsando_arriba<3 and sabe_uppercut)
										accion=ataca_uppercut;
										gravedad=-20;
										altura=1;
									elseif(tipo!=0)
										accion=ataca_suelo;
									else
										tres_ataques++;
										if(tres_ataques==3 and sabe_ataque_fuerte)
											accion=ataca_fuerte;
											ataca_fuerte_sin_retraso=1;
											tres_ataques=0;
										else
											accion=ataca_suelo;
											ultimo_ataque=0;
										end
									end
								end
							end
						else
							sonido(3,0);
							accion=lanza_objeto;
						end
					end

					//MOVIMIENTO
					if(sabe_moverse)
						if(p[jugador].botones[b_arriba] xor p[jugador].botones[b_abajo])
							if(p[jugador].botones[b_arriba])
								y_inc-=3;
							else //abajo
								y_inc+=3;
							end
						end
						
						if(p[jugador].botones[b_izquierda] xor p[jugador].botones[b_derecha])
							if(no_pulsando_lado==1 and accion==camina and sabe_correr)
								accion=corre; 
							end
							if(p[jugador].botones[b_izquierda])
								x_inc-=3;
								if(!en_moto) flags=1; end
							else //derecha
								x_inc+=3;
								flags=0;
							end
						end
					end
					
					//defenderse
					if(p[jugador].botones[b_3] and !p[jugador].botones[b_1] and sabe_defenderse)
						accion=defiende;
						if(lleva_objeto>0)
							objeto(x,y_base,altura-100,lleva_objeto,0,flags);
							lleva_objeto=0;
						elseif(lleva_objeto==-1)
							if(exists(id_enemigo_cogido))
								id_enemigo_cogido.x=x;
								id_enemigo_cogido.y=y-25;
								id_enemigo_cogido.z=z-1;
								id_enemigo_cogido.y_base=y_base;
								id_enemigo_cogido.x_inc=0;
								id_enemigo_cogido.y_inc=0;
								id_enemigo_cogido.flags=flags;
								id_enemigo_cogido.altura=-30;
								id_enemigo_cogido.gravedad=-10;
								id_enemigo_cogido.accion=herido_grave;
								id_enemigo_cogido.herida=30;
								id_enemigo_cogido=0;
								lleva_objeto=0;
							else
								id_enemigo_cogido=0;
								lleva_objeto=0;
							end
						end
					end
				end
														
				//salto
				if(p[jugador].botones[b_2] and (accion==quieto or accion==camina or accion==corre) and sabe_saltar)
					if(pulsando_salto==0)
						pulsando_salto=1;
						gravedad=-24;
						altura=1;
					end
				else
					pulsando_salto=0;
				end

				//reducción de inercias
				if(!(sabe_ataque_especial and accion==ataca_area))
					friccioname();
				end
				
			else //EN EL AIRE (SALTO)
				//ataque aereo
				if((accion==quieto or accion==camina or accion==corre or accion==salta))
					if(p[jugador].botones[b_1] and pulsando_ataque1==0 and sabe_ataca_aire)
						pulsando_ataque1=1;
						if(lleva_objeto==0)
							if(p[jugador].botones[b_3] and sabe_ataque_especial and p[jugador].vida>20)
								accion=ataca_area;
								sonido(3,3);
								if(invencibilidad==0)
									p[jugador].vida-=10;
								end
							elseif(p[jugador].botones[b_arriba] and pulsando_arriba<3 and gravedad<0 and sabe_uppercut)
								accion=ataca_uppercut;
								gravedad=-20;
							else
								if(p[jugador].botones[b_derecha] and flags==0 and no_pulsando_lado<3)
									x_inc+=40;
								elseif(p[jugador].botones[b_izquierda] and flags==1 and no_pulsando_lado<3)
									x_inc-=40;
								elseif(p[jugador].botones[b_abajo] and pulsando_abajo<3)
									gravedad=10;
								end
								accion=ataca_aire;
							end
							sonido(3,0);
						else
							accion=lanza_objeto;
							sonido(3,0);
						end
					end

					if(p[jugador].botones[b_arriba] xor p[jugador].botones[b_abajo])
						if(p[jugador].botones[b_arriba])
							y_inc-=1;
						else //izquierda
							y_inc+=1;
						end
					end
					if(p[jugador].botones[b_izquierda] xor p[jugador].botones[b_derecha])
						if(p[jugador].botones[b_izquierda])
							x_inc-=1;
							if(en_moto)
								angle_inc+=3;
							else
								flags=1;
							end
						else //derecha
							x_inc+=1;
							flags=0;
							if(en_moto)
								angle_inc-=3;
							end
						end
					end
				end

				if(evento_hamburguesa==7) //gravedad lunar
					gravedad+=1;
				else
					gravedad+=2;
				end
				altura+=gravedad;
				if(altura>1)
					if(en_moto)
						if(angle>360000) angle-=360000; end
						if(angle<0) angle+=360000; end
						if(angle>315000 or angle<45000)
							altura=0;
							gravedad=0;
							accion=quieto;
							angle_inc=0;
							angle=0;
						else
							nube_humo(x,y+50,z,0,0,100);
							if(angle==180000)
								angle_inc=18;
							else
								angle_inc=(angle-180000)/10000;
								x_inc+=angle_inc/2;
							end
							gravedad=-20;
							altura=-1;
							if(angle>135000 and angle<225000)
								herida=5;
							end
						end
					else
						altura=0;
						gravedad=0;
						accion=quieto;
					end
				end
			end
			
			//no herido, común de suelo y aire
			if(accion!=herido_leve and accion!=herido_grave and accion!=muere)
				if(accion==corre)
					inercia_max_x_actual=inercia_max_x*2;
					inercia_max_y_actual=inercia_max_y*2;
				else
					if(inercia_max_x_actual>inercia_max_x) inercia_max_x_actual--; else inercia_max_x_actual=inercia_max_x; end
					if(inercia_max_y_actual>inercia_max_y) inercia_max_y_actual--; else inercia_max_y_actual=inercia_max_y; end
				end
				inercia_maxima(inercia_max_x_actual,inercia_max_x_actual); 
			end
			//FIN DE ACCIONES SIN ESTAR HERIDO
		elseif(en_moto==0)
			//SIENDO HERIDO:
			if(accion!=herido_leve and accion!=herido_grave and accion!=muere) //escisión!
				//se le cae el objeto
				if(lleva_objeto>0)
					objeto(x,y_base,altura-100,lleva_objeto,0,flags);
					lleva_objeto=0;
				elseif(lleva_objeto==-1)
					if(exists(id_enemigo_cogido))
						id_enemigo_cogido.x=x;
						id_enemigo_cogido.y=y-25;
						id_enemigo_cogido.z=z-1;
						id_enemigo_cogido.y_base=y_base;
						id_enemigo_cogido.x_inc=0;
						id_enemigo_cogido.y_inc=0;
						id_enemigo_cogido.flags=flags;
						id_enemigo_cogido.altura=-30;
						id_enemigo_cogido.gravedad=-10;
						id_enemigo_cogido.accion=herido_grave;
						id_enemigo_cogido.herida=30;
						id_enemigo_cogido=0;
						lleva_objeto=0;
					else
						id_enemigo_cogido=0;
						lleva_objeto=0;
						accion=quieto; //dafuq?
					end
				end
				p[jugador].vida-=herida;
				if(herida<20 and altura==0)
					sonido(1,0);
					accion=herido_leve;
					herida=7; //más retraso
				else
					sonido(2,0);
					if(sabe_herida_grave)
						accion=herido_grave;
					else
						accion=herido_leve;
					end
					if(herida>15)
						gravedad=-herida;
					else
						gravedad=-15;
					end
					if(flags==1)
						x_inc=herida/4;
					else
						x_inc=-herida/4;
					end
					altura=-1;
				end
				if(tipo!=0 and accion==herido_grave) herida=herida*2; end //doble de retraso al levantarse en los enemigos
				if(herida>120) herida=120; end
			else //está siendo herido
			
				//posibilidad de desquitarse con un ataque de área
				if(accion==herido_leve and p[jugador].botones[b_1] and pulsando_ataque1==0 and p[jugador].botones[b_3] and p[jugador].vida>30 and tipo==0)
					pulsando_ataque1=1;
					accion=ataca_area;
					sonido(3,3);
					herida=0;
					if(invencibilidad==0)
						p[jugador].vida-=10;
					end
				end
			
				if(altura==0)
					herida--;
				end
			end
			
			//recuperación aérea
			//if(accion==herido_grave and p[jugador].botones[b_arriba] and pulsando_arriba<3 and p[jugador].botones[b_2] and gravedad>0)
			if(accion==herido_grave and p[jugador].botones[b_2] and gravedad>4 and sabe_saltar)
				gravedad=-10;
				accion=quieto;
				herida=0;
			end

			altura+=gravedad;

			if(altura>1)
				if(tipo!=0) //golpe de los enemigos al caerse heridos
					p[jugador].vida-=gravedad/2;
				end
				nube_humo(x,y+30,z,0,0,100);
				altura=0;
				gravedad=0;
				graph=73;
				sonido(4,0);
			elseif(altura<0)
				if(evento_hamburguesa==7) //gravedad lunar
					gravedad+=1;
				else
					gravedad+=2;
				end
			else
				friccioname();
			end
		ELSE //HERIDO EN MOTO:
			if(accion!=muere)
				if(accion!=herido_leve)
					p[jugador].vida-=herida;
					sonido(2,0);
					accion=herido_leve;
					if(herida<7) herida=7; end //más retraso
					if(herida>15)
						gravedad+=-herida;
					else
						gravedad+=-15;
					end
					if(flags==1)
						x_inc=herida/4;
					else
						x_inc=-herida/4;
					end
					altura=-1;
					sonido(2,0);
					accion=herido_leve;
				else
					herida--;
				end
				if(altura>1)
					if(angle>360000) angle-=360000; end
					if(angle<0) angle+=360000; end
					if(angle>315000 or angle<45000)
						altura=0;
						gravedad=0;
						accion=quieto;
						angle_inc=0;
						angle=0;
					else
						if(angle==180000)
							angle_inc=18;
						else
							angle_inc=(angle-180000)/10000;
							x_inc+=angle_inc/2;
						end
						gravedad=-20;
						altura=-1;
						if(angle>135000 and angle<225000)
							herida=5;
						end
					end
				else
					if(evento_hamburguesa==7) //gravedad lunar
						gravedad+=1;
					else
						gravedad+=2;
					end
				end
			end
		end
		if(!p[jugador].botones[b_1]) pulsando_ataque1=0; end
		
		if(herida==0 and altura==0)
			if(accion==quieto and (x_inc!=0 or y_inc!=0) and sabe_moverse)
				if(abs(x_inc)>inercia_max_x and sabe_correr)
					accion=corre;
				else
					accion=camina;
				end
			elseif((accion==camina or accion==corre) and x_inc==0 and y_inc==0)
				accion=quieto;
			end
		end
		
		//pon animacion correspondiente a mi acción
		pon_animacion();
		
		//gestiones comunes de los personajes en juego
		if(jugador<10 or tipo>100)
			mueveme(encerrandome);
		else
			mueveme(sin_encerrarme);
		end
		
		//hacks para la animación de en_moto
		if(en_moto)
			if(tipo==0)
				flags=0;
			else
				flags=1;
			end
			if(accion==quieto)
				accion=camina;
				animacion=camina;
			end
			if(accion==camina and anim==17)
				anim=0; 
			end
		end
		animame();
		cuerpo();
		sombra();
		
		//gestion del objeto portado o siendo cogido
		if(lleva_objeto!=0 or accion==lanza_objeto)
			if(accion==lanza_objeto)
				if(anim<4) //preparándose para lanzar el objeto
					if(lleva_objeto>0)
						objeto_portado(x+(-30*hacia_que_lado),y-25,lleva_objeto);
					else
						if(exists(id_enemigo_cogido))
							id_enemigo_cogido.x=x+(-40*hacia_que_lado);
							id_enemigo_cogido.y=y-25;
							id_enemigo_cogido.z=z-1;
							id_enemigo_cogido.y_base=y_base;
							id_enemigo_cogido.x_inc=0;
							id_enemigo_cogido.y_inc=0;
							id_enemigo_cogido.flags=flags;
							id_enemigo_cogido.altura=0;
							id_enemigo_cogido.gravedad=0;
						else
							id_enemigo_cogido=0;
							lleva_objeto=0;
							accion=quieto; //dafuq?
						end
					end
				else
					if(anim==4) //lanza el objeto
						if(lleva_objeto>0)
							objeto(x,y_base,altura-80,lleva_objeto,x_inc+(10*hacia_que_lado),flags);
						else
							if(exists(id_enemigo_cogido))
								id_enemigo_cogido.x=x+(-40*hacia_que_lado);
								id_enemigo_cogido.y=y-25;
								id_enemigo_cogido.y_base=y_base;
								id_enemigo_cogido.z=z-1;
								id_enemigo_cogido.x_inc=x_inc+(10*hacia_que_lado);
								id_enemigo_cogido.flags=flags;
								id_enemigo_cogido.y_inc=y_inc;
								id_enemigo_cogido.altura=altura-80;
								id_enemigo_cogido.gravedad=-10;
								id_enemigo_cogido.accion=herido_grave;
								id_enemigo_cogido.herida=abs(x_inc)+40;
							end
						end
						id_enemigo_cogido=0;
						lleva_objeto=0;
					end
				end
				if(anim==7)
					accion=quieto;
				end
			elseif(accion==coge_objeto)
				if(anim<4)
					if(lleva_objeto>0)
						objeto_portado(x+(-20*hacia_que_lado),y+35,lleva_objeto);
					else
						if(exists(id_enemigo_cogido))
							id_enemigo_cogido.x=x+(-30*hacia_que_lado);
							id_enemigo_cogido.y=y+20;
							id_enemigo_cogido.z=z-1;
							id_enemigo_cogido.y_base=y_base;
							id_enemigo_cogido.flags=flags;
							id_enemigo_cogido.x_inc=0;
							id_enemigo_cogido.y_inc=0;
							id_enemigo_cogido.altura=0;
							id_enemigo_cogido.gravedad=0;
						else
							id_enemigo_cogido=0;
							lleva_objeto=0;
							accion=quieto; //dafuq?
						end
					end
				else
					if(anim==7)
						accion=quieto;
					end
				end
			else //porta
				if(lleva_objeto>0)
					objeto_portado(x,y-34,lleva_objeto);
				else
					if(exists(id_enemigo_cogido))
						id_enemigo_cogido.x=x;
						id_enemigo_cogido.y=y-80;
						id_enemigo_cogido.z=z-1;
						id_enemigo_cogido.y_base=y_base;
						id_enemigo_cogido.flags=flags;
						id_enemigo_cogido.x_inc=0;
						id_enemigo_cogido.y_inc=0;
						id_enemigo_cogido.altura=0;
						id_enemigo_cogido.gravedad=0;
					else
						id_enemigo_cogido=0;
						lleva_objeto=0;
						accion=quieto; //dafuq?
					end
				end
			end
		end		
		
		//GESTIÓN DE LOS ATAQUES (puntos de colisión de los ataques)
		if(accion==ataca_suelo)
			if(anim<4)
				if(tipo==0) ataque(x+(15*hacia_que_lado),y-15,fpg_general,1,fuerza_ataque,20,jugador); end
			else
				if(en_moto)
					ataque(x+(70*hacia_que_lado),y-15,fpg_general,1,fuerza_ataque*1.5,20,jugador);
				else
					ataque(x+(45*hacia_que_lado),y-15,fpg_general,1,fuerza_ataque*1.5,20,jugador);
				end
			end
			if(anim==7)
				accion=quieto;
			end
		end
		if(accion==ataca_fuerte)
			if(ataca_fuerte_sin_retraso and anim<8) anim=8; end
			if(graph==142 or graph==143)
				ataque(x+(40*hacia_que_lado),y,fpg_general,3,fuerza_ataque*2.5,20,jugador);
			end
			if(anim==15)
				ataca_fuerte_sin_retraso=0;
				accion=quieto; 
			end
		end
		if(accion==ataca_uppercut)
			y_inc=0;
			if(flags) x_inc-=2; else x_inc+=2; end
			if(gravedad<0)
				ataque(x+(40*hacia_que_lado),y,fpg_general,3,fuerza_ataque*1.7,20,jugador);
			else
				accion=quieto;
			end
		end
		if(accion==ataca_aire)
			ataque(x+(40*hacia_que_lado),y,fpg_general,1,fuerza_ataque*2,15,jugador);
		end
		if(accion==ataca_area)
			if(tipo==0)
				ataque(x,y,file,graph,fuerza_ataque*4,20,jugador);
				if(altura!=0) gravedad--; end
				if(anim==23)
					accion=quieto;
				end
			else //jefe2
			if(anim>30 and anim<175)
					estela();
					ataque(x,y,file,graph,fuerza_ataque,20,jugador);
					if(x_inc==0 or y_inc==0)
						if(rand(0,1)==0) x_inc=rand(-5,-2); else x_inc=rand(2,5); end
						if(rand(0,1)==0) y_inc=rand(-5,-2); else y_inc=rand(2,5); end
					else
						if(x_inc<0) x_inc--; else x_inc++; end
						if(y_inc<0) y_inc--; else y_inc++; end
					end
				else
					friccioname();
				end
				if(anim==200)
					accion=quieto;
				end
			end
		end
		if(accion==muere)
			angle=0;
			if(herida>3000)
				//dafuq
				if(evento_hamburguesa==1)
					explosion(x,y_base,altura,size);
				end

				if(tipo>100) break; end
				from alpha=255 to 0 step -15; frame; end
				if(jugador>10)

					enemigos--;
					if(rand(0,3)==0 and modo_juego==modo_historia)
						objeto(x,y_base,altura,objetos_aleatorios[rand(0,4)],0,flags);
					end
				end
				
				if(p[jugador].vidas>0 and jugador<10)
					tipo=0;
					frame(2000);
					renacimiento=1;
					p[jugador].vidas--;
					alpha=255;
					x_inc=0;
					y_inc=0;
					herida=0;
					accion=0;
					x=id_camara.x;
					y_base=130+40*jugador;
					y=-500;
					altura=-300;
					p[jugador].vida=100;
					frame;
					renacimiento=0;
				else
					enemigos_matados++;
					p[jugador].juega=0;
					frame;
					return;
				end
			end
		end
		if(tipo==5) estela(); end
		frame;
	end
	
	if(tipo>100)
		if(tipo==102) //jefe2
			graph=151;
			from i=0 to 90; frame; end
			graph=73;
			from i=0 to 30; frame; end
			graph=211;
			funde_grafico_in(file,212);
			while(exists(type funde_grafico_in)) frame; end
			from i=0 to 90; frame; end
			graph=213;
			from i=0 to 90; frame; end
		end
		if(tipo==104) //jefe4
			graph=211;
			from i=0 to 90; frame; end
			graph=212;
			from i=0 to 30; frame; end
			graph=213;
			from i=0 to 30; frame; end
			graph=214;
			from i=0 to 30; frame; end
			graph=215;
			from i=0 to 90; frame; end
			enemigos--;
		end
		enemigos--;
				
		if(jugador==100)
			ganando=1;
			loop frame; end
		end
	end
End